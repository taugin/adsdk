apply plugin: 'com.android.library'

apply plugin: 'stringfog'
stringfog {
    // 这是加解密 key，可以自由定义。
    def random = ["a", "b", "c", "d", "e", "f", "0", "1", "2", "3", "4", "5", "6", "7", "8", "9"]
    Collections.shuffle(random)
    key random.join()
    println "fogkey : " + key
    // 开关
    enable true
    // 加解密库的实现类路径，需和上面配置的加解密算法库一致。
    implementation 'com.github.megatronking.stringfog.xor.StringFogImpl'
    // 指定需加密的代码包路径，可配置多个，未指定将默认全部加密。
    // fogPackages = ['com.xxx.xxx']
}
/**
 * 版本号与Activity和Provider之间的对应关系
 * versionName   ===> Vit-Activity.java
 * versionName.1 ===> Sec-Activity.java
 * versionName.2 ===> Tri-Activity.java
 */
android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionName "2.10.5"
        consumerProguardFiles 'proguard-rules-adsdk.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            println("output : " + output.toString())
        }
    }
}

dependencies {
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation('com.facebook.android:audience-network-sdk:5.8.0')
    // MoPub
    implementation('com.mopub:mopub-sdk:5.12.0@aar') {
        transitive = true
        exclude module: 'libAvid-mopub' // To exclude AVID
        exclude module: 'moat-mobile-app-kit'
        exclude group: 'androidx.fragment'
        exclude group: 'androidx.core'
    }
    // admob
    implementation 'com.google.android.gms:play-services-ads:18.0.0'
    // stringfog
    implementation 'com.github.megatronking.stringfog:xor:1.0.0'
}

// 发布aar
apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(rootProject.ext.publishUri))
            pom.version = android.defaultConfig.versionName
            pom.artifactId = "adsdk"
            pom.groupId = "com.hauyu"
            pom.packaging = "aar"
        }
    }
}